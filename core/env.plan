(def $defmacro
  ($vau name-params-body env
        (eval env (list def (first name-params-body) (cons $vau (rest name-params-body))))))

($defmacro quote value- env (first value-))

($defmacro if args env
  (eval env (bool (eval env (first args)) (nth args 1) (nth args 2))))

($defmacro wrap args definition-env
  ($vau arguments calling-env
        (operate calling-env
                 (eval definition-env (first args))
                 (operate calling-env list arguments))))

($defmacro $fn params-body definition-env
  (wrap (eval definition-env (list $vau (first params-body) (quote _) (nth params-body 1)))))

($defmacro $defn name-params-body env
  (eval env (list def (first name-params-body) (cons $fn (rest name-params-body)))))

($defn second args (nth (first args) 1))

($defn do args (last args))

($defmacro bind binding-and-value env
  (if (seqable? (first binding-and-value))
    (if (empty? (first binding-and-value))
      ()
      (if (= '& (first (first binding-and-value)))
        (eval env (list bind (second (first binding-and-value)) (second binding-and-value)))
        (do
          (eval env (list bind (first (first binding-and-value)) (first (second binding-and-value))))
          (eval env (list bind (rest (first binding-and-value)) (rest (second binding-and-value)))))))
    (eval env (cons def binding-and-value))))
